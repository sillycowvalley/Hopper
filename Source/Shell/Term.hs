program Term
{
    uses "/Source/System/System"
    uses "/Source/System/Serial"
    uses "/Source/System/Keyboard"
    uses "/Source/System/Screen"
    uses "/Source/System/Clipboard"
    
    #define JSON_EXPRESS // .code and .json are generated by us so assume .json files have no errors
    uses "/Source/Compiler/JSON/JSON"
    
    <string, string> debugOptions;
    string LoadBaud()
    {
        // load options
        string baud = "56700"; // default
        string optionsPath = Path.MakeOptions("Debugger.options");
        if (File.Exists(optionsPath))
        {
            <string, variant> dict;
            if (JSON.Read(optionsPath, ref dict))
            {
                debugOptions = dict["debugoptions"];
                if (debugOptions.Contains("baudRate"))
                {
                    baud = debugOptions["baudRate"];
                }
            }
        }
        return baud;
    }
    
    bool DoConnect()
    {
        bool success = false;
        string baudRate = LoadBaud();
        <string> ports = Serial.Ports;
        uint i;
        char defCh;
        PrintLn("COM Ports:");
        foreach (var port in ports)
        {
            i++;
            string content = "  " + (i.ToString()).LeftPad(' ', 3) + ": " + port;
            if (i == ports.Count)
            {
                content = content + " (default)";
                defCh = char(48+i);
            }
            PrintLn(content);
        }
        PrintLn("Press number to select port (or <enter> for default)");
        Key key = ReadKey();
        char ch = Keyboard.ToChar(key);
        if (key == Key.Enter)
        {
            ch = defCh;
        }
        if ((ch >= '1') && (ch <= '9'))
        {
            i = uint(ch) - 49;
            if (i < ports.Count)
            {
                string port = (ports[i]).Replace("COM", "");
                uint iport;
                if (UInt.TryParse(port, ref iport))
                {
                    Serial.Connect(iport, baudRate);
                    PrintLn("Connected to COM" + iport.ToString() + " at " + baudRate + " baud, (<Alt><F4> to exit)");
                    success = true;
                }
            }
        }
        
        
        return success;
    }
    string keyboardBuffer;
    /*
    AppendClipboard()
    {
        keyboardBuffer = keyboardBuffer + Clipboard.GetText();
    }
    */
    AppendClipboard()
    {
        string clipText = Clipboard.GetText();
        clipText = clipText.Replace("\r\n", "\n");
        keyboardBuffer = keyboardBuffer + clipText;
    }
    
    Hopper()
    {
        if (!DoConnect())
        {
            return;
        }
        
        Screen.ShowCursor = true;
        loop
        {
            if ((Keyboard.IsAvailable) || (keyboardBuffer.Length != 0))
            {
                char ch; 
                char maker;   
                bool exitKey;
                if (keyboardBuffer.Length != 0)
                {
                    ch = keyboardBuffer[0];
                    keyboardBuffer = keyboardBuffer.Substring(1);
                    Serial.WriteChar(ch);
                }
                else if (Keyboard.IsAvailable)
                {
                    Key key = Keyboard.ReadKey();
                    
                    // convert <right><click> in console area to <ctrl><V>
                    if ((key == Key.ClickRight) && Keyboard.ClickUp)
                    {
                        key = Key.ControlV;
                    }
                    
                    if (key == Key.ControlV)
                    {
                       if (Clipboard.HasText)
                       {
                           AppendClipboard();
                           continue;
                       }
                    }
                    else if (key == (Key.Alt | Key.F4))
                    {
                        exitKey = true;
                    }
                    ch = Keyboard.ToSerial(key, ref maker);
#ifdef PD6502
                    if (ch == char(0x0A)) { ch = char(0x0D); }
#endif
                    if (maker != char(0))
                    {
                        Serial.WriteChar(maker);
                    }
                    Serial.WriteChar(ch);
                }
                
                Delay(1);
                if (exitKey)
                {
                    Screen.ShowCursor = false;
                    Screen.PrintLn();
                    break;
                }
            }
            if (Serial.IsAvailable)
            {
                Screen.ShowCursor = false;
                char pch;
                while (Serial.IsAvailable)
                {
                    char ch = Serial.ReadChar();
                    if (ch == Char.EOL)
                    {
                        Screen.PrintLn();
                    }
                    else if (ch == Char.Formfeed)
                    {
                        Screen.Clear();
                    }
                    else if (ch == Char.Backspace)
                    {
                        Screen.Print(ch);
                    }
                    else if ((byte(ch) < 32) || (byte(ch) > 127))
                    {
                    }
                    else
                    {
                        Screen.Print(ch);
                    }
                    pch = ch;
                }
                Screen.ShowCursor = true;
            }
        }
        
        Serial.Close();
    }
}
