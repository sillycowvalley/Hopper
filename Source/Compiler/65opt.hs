program Optimize
{
    #define JSON_EXPRESS // .code and .json are generated by us so assume .json files have no errors
    #define DIAGNOSTICS  
    #define ASSEMBLER
      
    uses "/Source/System/System"
    uses "/Source/System/Screen"
    uses "/Source/System/Keyboard"
    
    uses "JSON/JSON"
    uses "JSON/Code"
    
    uses "Tokens/Token"
    uses "Tokens/Scanner"
    uses "Tokens/Parser"
    
    uses "CodeGen/Asm6502"
    
    uses "CodeModel/AsmPoints"
    
    uses "Symbols"
    
    bool verbose;
    bool showSizes;
    bool isExperimental;
    bool isTiggerC;
    bool IsExperimental { get { return isExperimental; }}
    bool IsTiggerC      { get { return isTiggerC; }}
    
    <uint,bool> methodsCalled;
    
    uint romSize;
    uint romAddress;
    
    uint irqIndex;
    bool irqExists;
    uint nmiIndex;
    bool nmiExists;
    
    
    <string> outputLinesRemoved;
    <string> outputLinesSizes;
    uint     totalBytesRemoved;
    uint     totalMethodsRemoved;
    uint     totalMethodBytes;
    
    
    const uint progressSteps = 256;
    uint progressInstructions;
    ProgessNudge()
    {
        progressInstructions++;
        if ((progressInstructions % progressSteps) == 0)
        {
            Parser.ProgressTick("o"); // optimizer
        }
    }
    
    CheckTarget()
    {
        romSize = 0x8000;
        if (Symbols.DefineExists("CPU_6502"))
        {
            Architecture = CPUArchitecture.M6502;
        }
        if (Symbols.DefineExists("CPU_65C02S"))
        {
            Architecture = CPUArchitecture.W65C02S;
        }
        if (Symbols.DefineExists("CPU_65C02"))
        {
            Architecture = CPUArchitecture.W65C02;
        }
        if (Symbols.DefineExists("CPU_65UINO"))
        {
            Architecture = CPUArchitecture.M6502;
            romSize = 0x1000;
        }
        if (Symbols.DefineExists("ROM_48K"))
        {
            romSize = 0xC000;
        }
        if (Symbols.DefineExists("ROM_32K"))
        {
            romSize = 0x8000;
        }
        if (Symbols.DefineExists("ROM_16K"))
        {
            romSize = 0x4000;
        }
        if (Symbols.DefineExists("ROM_8K"))
        {
            romSize = 0x2000;
        }
        if (Symbols.DefineExists("ROM_4K"))
        {
            romSize = 0x1000;
        }
        if (Symbols.DefineExists("ROM_1K"))
        {
            romSize = 0x0400;
        }
        if (Symbols.DefineExists("TIGGERC"))
        {
            isTiggerC = true;
        }
        if (Symbols.DefineExists("EXPERIMENTAL"))
        {
            isExperimental = true;
        }
        long startAddress = 0x10000 - romSize; 
        romAddress = uint(startAddress);
    }
    
    bool RemoveUnreachableMethods()
    {
        if (verbose)
        {
            ReportUnreachable();
        }
        
        bool removed;
        <string, <string,variant> > debugSymbols = GetDebugSymbols();
        <string, <string,variant> > newDebugSymbols;
        foreach (var methodCall in methodsCalled)
        {
            uint methodIndex = methodCall.key;
            if (methodCall.value || (methodIndex == 0) || (irqExists && (methodIndex == irqIndex)) || (nmiExists && (methodIndex == nmiIndex)) || Symbols.IsNoOpt(methodIndex))
            {
                 string index = "0x" + methodIndex.ToHexString(4);
                 newDebugSymbols[index] = debugSymbols[index];
            }
            else
            {
                removed = true;
            }
        }
        if (removed)
        {
            if (debugSymbols.Contains("globals"))
            {
                newDebugSymbols["globals"] = debugSymbols["globals"];
            }
            SetDebugSymbols(newDebugSymbols);
            
        }
        return removed;
    }
    
    ReportMethodSizes()
    {
        outputLinesSizes.Clear();
        totalMethodBytes = 0;
        
        foreach (var methodCall in methodsCalled)
        {
            uint methodIndex = methodCall.key;
            bool isCalled = methodCall.value || 
                            (methodIndex == 0); // "main" is an exception
            <byte> code = Code.GetMethodCode(methodIndex);
            uint  sizeInBytes = code.Count;
            string methodName = Code.GetMethodName(methodIndex);
            string sizeString = sizeInBytes.ToString();
            if (!isCalled)
            {
                continue;
            }
            outputLinesSizes.Append("  " + methodName.Pad(' ', 40) + sizeString.LeftPad(' ', 5));  
            totalMethodBytes = totalMethodBytes +  sizeInBytes;
        }
    }
    ReportUnreachable()
    {
        foreach (var methodCall in methodsCalled)
        {
            uint methodIndex = methodCall.key;
            bool isCalled = methodCall.value || 
                            (methodIndex == 0); // "main" is an exception
            <byte> code = Code.GetMethodCode(methodIndex);
            uint sizeInBytes = code.Count;
            string methodName = Code.GetMethodName(methodIndex);
            string sizeString = sizeInBytes.ToString();
            if (isCalled)
            {
                continue;
            }
            outputLinesRemoved.Append("  " + methodName.Pad(' ', 40) + sizeString.LeftPad(' ', 5));  
            totalBytesRemoved = totalBytesRemoved +  sizeInBytes;
            totalMethodsRemoved++;
        }
    }
    
    <uint, uint>      ijmpOnlyMethods;       // <methodIndex, targetMethodIndex>
    <uint, <OpCode> > smallMethodOpCodes;    // <methodIndex, <OpCode>>
    <uint, <uint> >   smallMethodOperands;   // <methodIndex, <uint>>
    <uint, <uint> >   smallMethodLengths;    // <methodIndex, <uint>>
    
    GatherSmallMethodsIntel()
    {
        <uint> indices = Code.GetMethodIndices();
        foreach (var methodIndex in indices)
        {
            uint size         = AsmPoints.Load(methodIndex, "gathering iJMP intel");
            uint instructions = AsmPoints.GetInstructionCount();
            // Check if method contains only a single iJMP instruction
            if ((instructions == 1) && 
                (AsmPoints.GetInstructionAt(0) == AsmPoints.GetOpcodeiJMP()))
            {
                uint   targetMethodIndex = AsmPoints.GetOperandAt(0);
                ijmpOnlyMethods[methodIndex] = targetMethodIndex;
            }
            if (size <= 4) // including RTS
            {
                if (AsmPoints.GetInstructionAt(instructions-1) == AsmPoints.GetOpcodeRTS())
                {
                    //string smallMethodName = Code.GetMethodName(methodIndex);
                    //Print(" " + smallMethodName + "[" + size.ToString() + "]");
                    <OpCode> codes;
                    <uint> operands;
                    <uint> lengths;
                    for (uint i=0; i < instructions-1; i++)
                    {
                        OpCode opCode = AsmPoints.GetInstructionAt(i);
                        codes.Append(opCode);
                        uint operand  = AsmPoints.GetOperandAt(i);
                        operands.Append(operand);
                        uint length   = AsmPoints.GetLengthAt(i);
                        lengths.Append(length);
                        //string name = Asm6502.GetName(opCode);
                        //Print("," + name);
                    }
                    smallMethodOpCodes [methodIndex] = codes;
                    smallMethodOperands[methodIndex] = operands;
                    smallMethodLengths [methodIndex] = lengths;
                }
            }
        }
    }
    
    bool Optimize(uint pass, ref long codeBefore, ref long codeAfter)
    {
        //PrintLn("Optimize: " + pass.ToString());
        
        bool modified = false;
        <uint> indices = Code.GetMethodIndices();
        methodsCalled.Clear();
        <uint, bool> methodsWalked;
        foreach (var index in indices)
        {
            methodsCalled[index] = false;
            methodsWalked[index] = false;
        }
        codeAfter = 0;
        
        // Gather intel about:
        // - iJMP-only method
        // - methods <= 4 bytes long
        ijmpOnlyMethods.Clear();
        smallMethodOpCodes.Clear();
        smallMethodOperands.Clear();
        smallMethodLengths.Clear();
        if (pass > 0)
        {
            GatherSmallMethodsIntel();
        }
        
        AsmPoints.Reset(); 
        
        uint methodIndex = 0; // "main"
        methodsCalled[methodIndex] = true; // "main"
        if (irqExists)
        {
            methodsCalled[irqIndex] = true;
        }
        if (nmiExists)
        {
            methodsCalled[nmiIndex] = true;
        }
        
        loop
        {
            methodsWalked[methodIndex] = true;
            uint size = AsmPoints.Load(methodIndex, "before pass " + pass.ToString());
            if (pass == 0)
            {
                codeBefore += size;
            }
            
            AsmPoints.MarkInstructions();
            
            // BRA|JMP to BRA|JMP -> BRA|JMP
            if (AsmPoints.OptimizeJMPJMP())
            {
                modified = true;
            }
            
            // removing JMP -> JMP + 1, can cause more short JumpToJump's to work
            if (AsmPoints.OptimizeRemoveJMPJMPs())
            {
                modified = true;
            }
            if ((Architecture == CPUArchitecture.W65C02S) || (Architecture == CPUArchitecture.W65C02))
            {
                // JMP -> BRA
                if (AsmPoints.OptimizeJMP())
                {
                    modified = true;
                }
                if (AsmPoints.OptimizeLDASTAtoSTZ())
                {
                    modified = true;
                }
                if (Architecture == CPUArchitecture.W65C02S)
                {
                    if (AsmPoints.OptimizeSMBandRMB())
                    {
                        modified = true;
                    }
                }
                // BEQ BRA -> BNE (for example)
                if (AsmPoints.OptimizeBEQBRA())
                {
                    modified = true;
                }
            }
            else
            {
                // BEQ JMP -> BNE (for example)
                if (AsmPoints.OptimizeBEQJMP())
                {
                    modified = true;
                }   
            }
            
            // BRA|JMP to RTS - > RTS
            if (AsmPoints.OptimizeJMPRTS())
            {
                modified = true;
            }
            // RTS|RTS to RTS - > RTS
            if (AsmPoints.OptimizeRTSRTS())
            {
                modified = true;
            }
            
            if (irqExists && (methodIndex == irqIndex))
            {
                // skip    
            }
            else if (nmiExists && (methodIndex == nmiIndex))
            {
                // skip    
            }
            else // TODO: // JSR|RTI - > iJMP .. RTI
            {
                // JSR|RTS - > iJMP
                if (AsmPoints.OptimizeJSRRTS())
                {
                    modified = true;
                }
            }
            if (ijmpOnlyMethods.Count > 0)
            {
                if (AsmPoints.OptimizeiJMPOnlyCallsInMethod(ijmpOnlyMethods))
                {
                    modified = true;
                }
            }
            if (!IsTiggerC)
            {
                if (AsmPoints.OptimizeTrip65())
                {
                    modified = true;
                }
            }
            if (AsmPoints.OptimizeCMP())
            {
                modified = true;
            }
            if (AsmPoints.OptimizeCLCBCS())
            {
                modified = true;
            }
            if (smallMethodOpCodes.Count > 0)
            {
                if (AsmPoints.OptimizeInlineSmallMethods(smallMethodOpCodes, smallMethodOperands, smallMethodLengths))
                {
                    modified = true;
                }
            }
            if (false && AsmPoints.OptimizeHunt())
            {
                modified = true;
            }
                       
            if (IsTiggerC && (pass > 1))
            {
                // start with peephole style optimizations
                if (AsmPoints.OptimizePeep())
                {
                    modified = true;
                }
                if (AsmPoints.OptimizeOver())
                {
                    modified = true;
                }
                if (AsmPoints.OptimizeTrip())
                {
                    modified = true;
                }
                if (AsmPoints.OptimizeQuad())
                {
                    modified = true;
                }
                if (AsmPoints.OptimizeSextet())
                {
                    modified = true;
                }
                if (AsmPoints.OptimizeOctet())
                {
                    modified = true;
                }
                if (AsmPoints.OptimizeTen())
                {
                    modified = true;
                }
            }
            
            
            if (!modified)
            {
                // remove NOPs even if there were no other optimizations
                modified = AsmPoints.OptimizeNOPExists();
            }
            
            
            
            // TODO:
            //
            // 1. check for LDA|X|Y #0 followed by STA|X|Y and replace with STZ on W65C02S
            //
            // 2. CMP #0 after LDA, LDX, LDY, INC, INX, INY, DEC, DEX, DEY, INA, DEA, AND, ORA, EOR, ASL, LSR, ROL, 
            //              ROR, PLA, PLX, PLY, SBC, ADC, TAX, TXA, TAY, TYA, and TSX
            // is redundant if checking Z or V. They all set the Z and V flags. C is a different story.
            
            if (pass > 0) // allow inlining to happen first
            {
                if (!Symbols.IsNoOpt(methodIndex) && AsmPoints.OptimizeUnreachableToNOP())
                {
                    modified = true;
                }
                // remove NOPs : should be last OptimizeXXX
                if (AsmPoints.OptimizeRemoveNOPs())
                {
                    modified = true;
                }
            }
            
            AsmPoints.CollectMethodCalls(methodsCalled);
            
            size = AsmPoints.Save();
            codeAfter = codeAfter + size;
            ProgessNudge();
            
            bool walkNextMethod;
            foreach (var kv in methodsCalled)
            {
                bool isCalled = kv.value;
                if (isCalled)
                {
                    uint iMethod = kv.key;
                    if (!methodsWalked.Contains(iMethod))
                    {
                        PrintLn("Pass:" + pass.ToString() + " " + iMethod.ToHexString(4));
                    }
                    if (!methodsWalked[iMethod])
                    {
                        // called but not yet walked
                        methodIndex = iMethod;
                        walkNextMethod = true;
                        break;
                    }
                }
            }
            if (!walkNextMethod) // no more to walk
            {
                break;
            }
        } // loop
        
        if (RemoveUnreachableMethods())
        {
            modified = true;
        }
        if (showSizes)
        {
            ReportMethodSizes();
        }
        methodsCalled.Clear(); // just to be sure 
        
        if ((pass == 0) && !modified)
        {
            // no optimizations were done on pass 0 which means we need at least a 2nd pass for
            // removing unreachable code
            modified = true; 
        }
        return modified;
    }
    
    BadArguments()
    {
        PrintLn("Invalid arguments for AsmOpt:");
        PrintLn("  ASMOPT <code file>");
        PrintLn("    -g <c> <r> : called from GUI, not console");
        PrintLn("    -v         : verbose output");
        PrintLn("    -t         : method size after optimization");
        PrintLn("    -x         : experimental");
    }
    
    Hopper()
    {
        bool success = false;
        loop
        {
            <string> rawArgs = System.Arguments;
            <string> args;
            for (uint iArg = 0; iArg < rawArgs.Count; iArg++)
            {
                string arg = rawArgs[iArg];
                if ((arg.Length >= 2) && (arg[0] == '-'))
                {
                    arg = arg.ToLower();
                    switch (arg)
                    {
                        case "-v":
                        {
                            verbose = true;
                        }
                        case "-t":
                        {
                            showSizes = true;
                        }
                        case "-x":
                        {
                            isExperimental = true;
                        }
                        case "-g":
                        {
                            uint col;
                            uint row;
                            iArg++;
                            if (UInt.TryParse(rawArgs[iArg], ref col))
                            {
                            }
                            iArg++;
                            if (UInt.TryParse(rawArgs[iArg], ref row))
                            {
                            }
                            Parser.SetInteractive(byte(col), byte(row));
                        }
                        default:
                        {
                            args.Clear();
                            break;
                        }
                    }
                }
                else
                {
                    args.Append(arg);
                }
            }
          
            if (args.Count != 1)
            {
                BadArguments();
                break;
            }
            
            string ext = ".code";
            string codePath = args[0];
            if (!File.Exists(ref codePath, ref ext, "/Debug/Obj/"))
            {
                BadArguments();
            }
            
            long startTime = Millis;
            loop
            {
                string extension = Path.GetExtension(codePath);
                if (!Code.ParseCode(codePath, true, true))
                {
                    break;
                }
                long codeBefore;
                long codeAfter;
                string optPath = codePath;
                string symbolsPath = codePath.Replace(extension, ".sym");
                string verbosePath = codePath.Replace(extension, ".txt");
                
                if (File.Exists(symbolsPath))
                {
                    Symbols.New();
                    if (Symbols.Import(symbolsPath))
                    {
                        CheckTarget();
                    }
                }
                
                string hopperMethod = Code.GetMethodName(0);
                string irqMethod = hopperMethod.Replace(".Hopper", ".IRQ");
                string nmiMethod = hopperMethod.Replace(".Hopper", ".NMI");
                if (Code.MethodExists(irqMethod))
                {
                    irqIndex = Code.GetMethodIndex(irqMethod);
                    irqExists  = true;
                }
                if (Code.MethodExists(nmiMethod))
                {
                    nmiIndex = Code.GetMethodIndex(nmiMethod);
                    nmiExists  = true;
                }
                
                uint pass = 0;
                loop
                {
                    if (!Optimize(pass, ref codeBefore, ref codeAfter))
                    {
                        if (pass >= 3) // at least 4 passes
                        {
                            break;
                        }
                    }
                    pass++;
                }
                               
                File.Delete(optPath);
                if (!Code.ExportCode(optPath)) // after
                {
                    break;
                }
                success = true;
                if (!Parser.IsInteractive())
                {
                    PrintLn();
                    if (verbose || showSizes)
                    {
                        file logFile = File.Create(verbosePath);
                        bool addNewLine;
                        if (outputLinesSizes.Count != 0)
                        {
                            string content = "Method sizes after optimization:";
                            logFile.Append(content + Char.EOL);
                            PrintLn(content);
                            uint count;
                            foreach (var str in outputLinesSizes)
                            {
                                logFile.Append(str + Char.EOL);
                                PrintLn(str);
                                count++;
                            }
                            string space;
                            string sizeString = totalMethodBytes.ToString();
                            content = space.Pad(' ', 42) + sizeString.LeftPad(' ', 5) +
                                      " (" + count.ToString() + " methods)";
                            logFile.Append(content + Char.EOL);
                            PrintLn(content);
                            addNewLine = true;
                        }
                        if (totalMethodsRemoved > 0)
                        {
                            if (addNewLine)
                            {
                                logFile.Append("" + Char.EOL);
                                PrintLn();
                            }
                            string content = "Unreachable Code (includes inlined and removed):";
                            logFile.Append(content + Char.EOL);
                            PrintLn(content);
                            foreach (var str in outputLinesRemoved)
                            {
                                logFile.Append(str + Char.EOL);
                                PrintLn(str);
                            }
                            string space;
                            string sizeString = totalBytesRemoved.ToString();
                            content = space.Pad(' ', 42) + sizeString.LeftPad(' ', 5) +
                                      " (" + totalMethodsRemoved.ToString() + " methods)";
                            logFile.Append(content + Char.EOL);
                            PrintLn(content);
                        }
                        logFile.Flush();
                    }
                    Print("Success, " + codeBefore.ToString() + "->" + codeAfter.ToString() + " bytes of code,", Colour.ProgressText, Colour.ProgressFace);
                    long elapsedTime = Millis - startTime;
                    float seconds = elapsedTime / 1000.0;
                    PrintLn("  " + seconds.ToString() + "s", Colour.ProgressHighlight, Colour.ProgressFace);
                }
                else
                {
                    Parser.ProgressDone();
                }
                
                break;
            }
            break;
        } // loop
        if (!success)
        {
            Diagnostics.SetError(0x0E);
        }   
    } // Hopper()
}
